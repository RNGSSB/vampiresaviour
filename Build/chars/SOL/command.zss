[Function Combo() ret]
let ret = 0;
if (stateType != A && ctrl) ||
	((stateNo = [200, 299] || stateNo = [400, 499]) && stateNo != 440 && moveContact) || #!= 440: Except for sweep kick
	(stateNo = 1310 || stateNo = 1330) { #From blocking
	let ret = 1;
}

#===============================================================================
# command code
#===============================================================================
[StateDef -1]

#============|IF roundState = 2|================================================
if roundState = 2 && !aiLevelF { #prevent attacks outside of fight state

let canCombo = call Combo();
ignoreHitPause{mapSet{map:"canCombo"; value: $canCombo}}

#-------------------------------------------------------------------------------
#Triple Kung Fu Palm (uses one super bar)
if command = "TripleKFPalm" && power >= 1000 && stateType != A &&
	(ctrl ||
		(hitDefAttr = SC, NA, SA, HA && stateNo != [3050, 3100] && moveContact) ||
		(stateNo = 1310 || stateNo = 1330)) { #From blocking
	changeState{value: 3000}
}

#-------------------------------------------------------------------------------
#Light Kung Fu Palm
if command = "QCF_x" && $canCombo {
	changeState{value: 1000}
}

#-------------------------------------------------------------------------------
#Run Fwd
if (command = "FF" || Map(b) && command != "holdback") && stateType != A && ctrl && stateNo != 100 {
	changeState{value: 100}
}

#-------------------------------------------------------------------------------
#Run Back
if (command = "BB" || Map(b) && command = "holdback" ) && stateType = S && ctrl {
	changeState{value: 105}
}

#-------------------------------------------------------------------------------
#Kung Fu Throw
if map(xy) && stateType = S && (ctrl || (stateNo >= 200 && stateNo <= 440 && time < 4)){
	changeState{value: 800}
}

#===============================================================================
#Resource Reset
if stateType = S || stateType = C {
mapSet{map:"airJumpCount"; value: 1}
mapSet{map: "airDashAttackBuffer"; value: 0}
mapSet{map: "airDashMacroBuffer"; value: 0}
sysVar(1) := 0;
}

#Jump
if stateNo = 210 || stateNo = 230 || stateNo = 430
|| stateNo = 600 || stateNo = 610 || stateNo = 630 || stateNo = 640{
call JumpCancel();
}

#Air Jump
if stateType = A && ctrl && map(airJumpCount) > 0 && map(airJumpInput) != 1{
	if command = "holdup" {
		changeState{value: 46}
	}
}

#Has pressed up for ground jump
if stateNo = 40 {
	if command = "up" || command = "holdup"  {
		mapSet{map:"airJumpInput"; value: 1}
	}
	else{
		mapSet{map:"airJumpInput"; value: 0}
	}
}

#Has stopped pressing any up direction while jumping
if stateType = A && ctrl{
	if (command = "release_up"|| command = "release_upfwd" || command = "release_upback") && command != "holdup"   {
		mapSet{map:"airJumpInput"; value: 0}
	}
}

#Air Dash Forward
if ((Map(FF) || command = "holdfwd" && Map(b)|| command != "holdfwd" && Map(b)) && facing = 1 || (Map(FF) || command = "holdback" && Map(b)|| command != "holdback" && Map(b)) && facing = -1) 
&& command != "holdback" && stateType = A && ctrl && map(airJumpCount) != 0 && command != "holddown" {
	if pos Y < -50 && vel y < 0 || vel y > 0{
		changeState{value: 110}
	}
}

#Air Dash Back
if (Map(BB) || command = "holdback" && Map(b)) && stateType = A && ctrl && map(airJumpCount) != 0 && command != "holddown" {
	if pos Y < -50 && vel y < 0 || vel y > 0 {
		changeState{value: 115}
	} 
}

#Buffered Button IAD
if map(airDashMacroBuffer) = 1 && map(airDashMacroDirection) = 1 && stateType = A && ctrl && map(airJumpCount) != 0
{
	if pos Y < -50 && vel y < 0 || vel y > 0{
		changeState{value: 110}
	}
}

#Buffered Button IADB
if map(airDashMacroBuffer) = 1 && map(airDashMacroDirection) = 0 && stateType = A && ctrl && map(airJumpCount) != 0
{
	if pos Y < -50 && vel y < 0 || vel y > 0{
		changeState{value: 115}
	}
}

#Bufferd Button IAD Check
if Map(b) && (stateNo = 50 || stateNo = 53 || stateNo = 40) && time <= 10 {
	mapSet{map:"airDashMacroBuffer"; value: 1}
	
	if command = "holdback"{
		mapSet{map:"airDashMacroDirection"; value: 0}
	}
	else if command = "holdfwd" || command != "holdback" && command != "holdfwd"
	{
		mapSet{map:"airDashMacroDirection"; value: 1}
	}
}

#Has stopped pressing any up direction while jumping
if stateType = A && ctrl{
	if (command = "release_up"|| command = "release_upfwd" || command = "release_upback") && command != "holdup"   {
		mapSet{map:"airJumpInput"; value: 0}
	}
}

#Throw Tech xd
if ctrl && (Map(x) || Map(y) || Map(z) || Map(a)) && Map(ThrowTechBuffer) != 1{
	MapSet{map: "ThrowTechBuffer"; value: 1}
	MapSet{map: "TechTimer"; value: gameTime}
}

if gameTime = Map(TechTimer) + 4 {
	MapSet{map: "ThrowTechBuffer"; value: 0}
}

#-------------------------------------------------------------------------------
#Hi parry
if map(xa) && command != "holddown" &&
	(stateType = S && (ctrl || time <= 3))  {
	changeState{value: 700}
}

#-------------------------------------------------------------------------------
#Low parry
if map(xa) && command = "holddown" &&
	(stateType = C && (ctrl || time <= 3))  {
	changeState{value: 710}
}

#-------------------------------------------------------------------------------
#Air Parry
if map(xa) &&
	(stateType = A && (ctrl || time <= 3))  {
	changeState{value: 720}
}

#-------------------------------------------------------------------------------
#5A
if map(x) && command != "holddown" &&
	((stateType = S && ctrl) ||
		((stateNo = 200 || stateNo = 400) && moveContact)) {
	changeState{value: 200}
}

#-------------------------------------------------------------------------------
#5B
if map(y) && command != "holddown" &&
	((stateType = S && ctrl) ||
		((stateNo = 200 || stateNo = 400 || stateNo = 410) && moveContact)) {
	changeState{value: 210}
}

#-------------------------------------------------------------------------------
#5C
if map(z) &&Command != "holddown" &&
	((stateType = S && ctrl) ||
		((stateNo = 200 || stateNo = 400 || stateNo = 410 || stateNo = 210) && moveContact)) {
	changeState{value: 230}
}

#-------------------------------------------------------------------------------
#5D
if map(a) &&Command != "holddown" &&
	((stateType = S && ctrl) ||
		((stateNo = 200 || stateNo = 230 || stateNo = 210 || stateNo = 400 || stateNo = 430 || stateNo = 410) && moveContact)) {
	changeState{value: 240}
}

#-------------------------------------------------------------------------------
#Taunt
if command = "start" && command != "holddown" && stateType != A && ctrl {
	changeState{value: 195}
}

#-------------------------------------------------------------------------------
#2A
if map(x) && command = "holddown" && 
((stateType = C && ctrl) ||
		((stateNo = 400) && moveContact)) {
	changeState{value: 400}
}

#-------------------------------------------------------------------------------
#2B
if map(y) && command = "holddown" &&
	((stateType = C && ctrl) ||
		((stateNo = 210 || stateNo = 200 || stateNo = 400) && moveContact)) {
	changeState{value: 410}
}

#-------------------------------------------------------------------------------
#2C
if map(z) && command = "holddown" &&
	((stateType = C && ctrl) ||
		((stateNo = 200 || stateNo = 400 || stateNo = 410 || stateNo = 210) && moveContact && animElemTime(3) > 0)) {
	changeState{value: 430}
}

#-------------------------------------------------------------------------------
#2D
if map(a) && command = "holddown" &&
	((stateType = C && ctrl) ||
		(stateNo = 400 && animElemTime(2) > 0) ||
		((stateNo = 430 || stateNo = 410 || stateNo = 200 || stateNo = 210 || stateNo = 230 || stateNo = 400) && moveContact && animElemTime(3) > 0)) {
	changeState{value: 440}
}

#-------------------------------------------------------------------------------
#jA
if map(x) &&
	((stateType = A && ctrl) ||
		(stateNo = 600 && moveContact) ||
		stateNo = 1350) { #Air blocking
	changeState{value: 600}
}

#-------------------------------------------------------------------------------
#jB
if map(y) &&
	((stateType = A && ctrl) ||
		((stateNo = 600) && moveContact) || #jump_x or jump_a
		stateNo = 1350) { #Air blocking
	changeState{value: 610}
}

#-------------------------------------------------------------------------------
#jC
if map(z) &&
	((stateType = A && ctrl) ||
	((stateNo = 600 || stateNo = 610) && moveContact) ||
		stateNo = 1350) { #Air blocking
	changeState{value: 630}
}

#-------------------------------------------------------------------------------
#jD
if map(a) &&
	((stateType = A && ctrl) ||
		((stateNo = 600 || stateNo = 630 || stateNo = 610) && moveContact) || #jump_x or jump_a
		stateNo = 1350) { #Air blocking
	changeState{value: 640}
}

#End of file: roundState = 2
#===============================================================================
}
